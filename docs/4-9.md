---
title: Step 10 - Running a *pipeline*
parent: Step by step
nav_order: 9
---

## Step 10 - Running a *pipeline*

We used the pipe `|` symbol to combine different steps in a *pipeline*
and we noticed that a `process` can do computations on parallel
branches. That's nice, but we have not yet given an example of running 2
processes, one after the other.

There are a few things we have to note before we go to an example:

1.  It's not possible to call the same process twice, a strange error
    occurs in that case[^2].
2.  If we want to pipe the output of one process as input of the next,
    the I/O signature needs to be exactly the same, so the `output` of
    the `process` should be a triplet as well.

``` groovy
// Step - 10
process process_step10a {
  input:
    tuple val(id), val(input), val(term)
  output:
    tuple val("${id}"), val(output), val("${term}")
  exec:
    output = input.toInteger() + term.toInteger()
}
process process_step10b {
  input:
    tuple val(id), val(input), val(term)
  output:
    tuple val("${id}"), val(output), val("${term}")
  exec:
    output = input.toInteger() - term.toInteger()
}
workflow step10 {
  Channel.from( [ 1, 2, 3 ] ) \
    | map{ el -> [ el.toString(), el, 10 ] } \
    | process_step10a \
    | process_step10b \
    | view{ it }
}
```

The result of this is that first 10 is added and then the same 10 is
subtracted again, which results in the same as the original. Please note
that the output contains 3 elements, also the `term` passed to the
`process`:

``` sh
> nextflow -q run . -entry step10
WARN: DSL 2 IS AN EXPERIMENTAL FEATURE UNDER DEVELOPMENT -- SYNTAX MAY CHANGE IN FUTURE RELEASE
[1, 1, 10]
[3, 3, 10]
[2, 2, 10]
```

We can configure the second `process` (subtraction) by adding an
additional `map` in the mix:

``` groovy
// Step - 10a
workflow step10a {
  Channel.from( [ 1, 2, 3 ] ) \
    | map{ el -> [ el.toString(), el, 10 ] } \
    | process_step10a \
    | map{ id, value, term -> [ id, value, 5 ] } \
    | map{ [ it[0], it[1], 5 ] } \
    | map{ x -> [ x[0], x[1], 5 ] } \
    | process_step10b \
    | view{ it }
}
```

Resulting in:

``` sh
> nextflow -q run . -entry step10a
WARN: DSL 2 IS AN EXPERIMENTAL FEATURE UNDER DEVELOPMENT -- SYNTAX MAY CHANGE IN FUTURE RELEASE
[1, 6, 5]
[2, 7, 5]
[3, 8, 5]
```

Please note that we define the closure in a different manner here, using
the special variable `it`. We could also write (to the same effect):

``` groovy
  ...
  | map{ x -> [ x[0], x[1], 5 ] } \
  ...
```

or even

``` groovy
  ...
  | map{ id, value, term -> [ id, value, 5 ] } \
  ...
```

